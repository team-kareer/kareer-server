plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.google.cloud.tools.jib' version '3.4.4'
}

group = 'org.sopt'
version = '0.0.1-SNAPSHOT'
description = 'Kareer'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // h2
    runtimeOnly 'com.h2database:h2'

    // Spring Data Jpa
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // Spring Security
    implementation 'org.springframework.boot:spring-boot-starter-security'
    testImplementation 'org.springframework.security:spring-security-test'

    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Spring OAuth2 Client
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Postgresql
    runtimeOnly 'org.postgresql:postgresql'

    // Test
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'

    // Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Selenium
    implementation 'org.seleniumhq.selenium:selenium-java:4.20.0'

    // Spring AI
    implementation 'org.springframework.ai:spring-ai-openai-spring-boot-starter:1.0.0-M6'

    // PDFBox
    implementation 'org.apache.pdfbox:pdfbox:2.0.27'

    // pgvector
    implementation 'org.springframework.ai:spring-ai-pgvector-store-spring-boot-starter:1.0.0-M6'

    // QueryDSL
    implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.1.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
}

// Q클래스 생성 경로 설정
def generated = 'src/main/generated'

tasks.withType(JavaCompile).configureEach {
    options.getGeneratedSourceOutputDirectory().set(file(generated))
}

// clean 시 generated 폴더 삭제
clean {
    delete file(generated)
}

jib {
    from {
        image = 'eclipse-temurin:17-jre-alpine'
    }

    to {
        image = "${System.getenv('ECR_REGISTRY')}/${System.getenv('ECR_REPOSITORY')}"

        tags = [
                System.getenv('GITHUB_SHA')
        ]
    }

    container {
        ports = ['8080']
        jvmFlags = [
                '-Duser.timezone=Asia/Seoul'
        ]
        creationTime = 'USE_CURRENT_TIMESTAMP'
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
